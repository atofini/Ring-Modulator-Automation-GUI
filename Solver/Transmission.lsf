
runsetup;

cwd = pwd;
transmission_directory = cwd + "/Database/Transmission/";
mode_directory = cwd + "/Database/Mode/";

# Running in transmission database directory
cd(transmission_directory);

start_freq = c/(start_wavelength);
stop_freq = c/(stop_wavelength);
# currently setting up so modulation covers half the ring
circ = 2*pi*radius + 2*L;

addelement("Optical Network Analyzer");
set("x position",250);
set("y position",0);
set("input parameter","start and stop");
set("start frequency",start_freq );
set("stop frequency",stop_freq);
set("number of points",500000);

addelement("Waveguide Coupler");
set("x position",300);
set("y position",219);
set("input parameter","table");
set("load from file",1);
filename_CC = transmission_directory + coupler_file + ".txt";
set("measurement filename 1",filename_CC);
set("measurement filename 2",filename_CC);

cd(mode_directory);
addelement("MODE Waveguide");
set("x position",200);
set("y position",400);
set("length",circ);
rotateelement("WGD_1");
filename_active = mode_directory + waveguide_file + ".ldf";
set("loss",propagation_loss); 
#set("loss",0); 
set("propagation loss",true); # this is actuall absorption and bend loss
set("ldf filename",filename_active);

cd(transmission_directory);
addelement("Optical Modulator Measured");
rotateelement("OM_1");
set("x position",400);
set("y position",350);
set("length",circ);
set("load from file",1);
neff_filename = transmission_directory + waveguide_file + ".txt";
set("measurement filename",neff_filename);


addelement("DC source");
set("x position",600);
set("y position",350);
flipelement("DC_1");

connect("ONA_1","output","C_1","port 1");
connect("ONA_1","input 1","C_1","port 3");
connect("WGD_1","port 1","C_1","port 2");
connect("WGD_1","port 2","OM_1","port 2");
connect("OM_1","port 1","C_1","port 4");
connect("DC_1","output","OM_1","modulation");

addsweep;
setsweep("sweep", "name", "voltage_sweep");
setsweep("voltage_sweep", "type", "Ranges");
setsweep("voltage_sweep", "number of points", N); 

# define the parameter thickness
para = struct;
para.Name = "voltage";
para.Parameter = "::Root Element::DC_1::amplitude";
para.Type = "Number";
para.Start = vmin;
para.Stop = vmax;
addsweepparameter("voltage_sweep", para);

# define results
result_1 = struct;
result_1.Name = "T";
result_1.Result = "::Root Element::ONA_1::input 1/mode 1/gain";



# add the results R & T to the sweep
addsweepresult("voltage_sweep", result_1);


# Saving file first
filename = "transmission_" + num2str(transmission_ID) + ".icp";
save(filename);
#Running sweep
runsweep("voltage_sweep");
result = getsweepresult("voltage_sweep", "T");
dataname =  "transmission_" + num2str(transmission_ID) + ".mat";
matlabsave(dataname, result);

#returning to starting directory
cd(cwd);
